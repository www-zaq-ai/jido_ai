# credo:disable-for-this-file Credo.Check.Refactor.LongQuoteBlocks

defmodule Jido.AI.Agent do
  @moduledoc """
  Base macro for Jido.AI agents with ReAct strategy implied.

  Wraps `use Jido.Agent` with `Jido.AI.Reasoning.ReAct.Strategy` wired in,
  plus standard state fields and helper functions.

  ## Usage

      defmodule MyApp.WeatherAgent do
        use Jido.AI.Agent,
          name: "weather_agent",
          description: "Weather Q&A agent",
          tools: [MyApp.Actions.Weather, MyApp.Actions.Forecast],
          system_prompt: "You are a weather expert..."
      end

  ## Options

  - `:name` (required) - Agent name
  - `:tools` (required) - List of `Jido.Action` modules to use as tools
  - `:description` - Agent description (default: "AI agent \#{name}")
  - `:tags` - Agent tags for discovery/classification (default: `[]`)
  - `:system_prompt` - Custom system prompt for the LLM
  - `:model` - Model alias or direct model spec (default: :fast, resolved via Jido.AI.resolve_model/1)
  - `:max_iterations` - Maximum reasoning iterations (default: 10)
  - `:max_tokens` - Maximum tokens per LLM response (default: `4096`)
  - `:streaming` - Whether to stream LLM responses (default: `true`)
  - `:request_policy` - Request concurrency policy (default: `:reject`)
  - `:tool_timeout_ms` - Per-attempt tool execution timeout in ms (default: 15_000)
  - `:tool_max_retries` - Number of retries for tool failures (default: 1)
  - `:tool_retry_backoff_ms` - Retry backoff in ms (default: 200)
  - `:stream_timeout_ms` - Stream consumer timeout in ms (default: auto-derived
    from tool_timeout_ms + 60s). How long to wait for events between coordinator
    updates. Increase for agents with slow LLM responses or long tool executions.
    `:stream_receive_timeout_ms` is accepted as a compatibility alias.
  - `:effect_policy` - Agent-level effect policy (default allow-list)
  - `:strategy_effect_policy` - Optional strategy-level narrowing policy (cannot broaden agent policy)
  - `:runtime_adapter` - Deprecated compatibility flag (delegated ReAct runtime is always enabled)
  - `:runtime_task_supervisor` - Optional Task.Supervisor used by delegated ReAct runtime
  - `:observability` - Observability options map
  - `:req_http_options` - Base Req HTTP options passed through to ReqLLM calls
  - `:llm_opts` - Additional ReqLLM generation options merged into ReAct LLM calls
  - `:request_transformer` - Module implementing per-turn ReAct request shaping
  - `:output` - Structured final-output config with an object-shaped Zoi or JSON Schema
  - `:tool_context` - Context map passed to all tool executions (e.g., `%{actor: user, domain: MyDomain}`).
    Must be literal data only — module aliases, atoms, strings, numbers, lists, and maps are permitted.
    Function calls, module attributes (`@attr`), and pinned variables (`^var`) raise `CompileError`.
    Runtime reserves `:state` (core Jido-compatible) for tool execution snapshots.
    User-provided values for that key are overwritten per request.
  - `:skills` - Additional skills to attach to the agent (TaskSupervisorSkill is auto-included)

  ## Generated Functions

  - `ask/2,3` - Async: sends query, returns `{:ok, %Request{}}` for later awaiting
  - `ask_stream/2,3` - Async: sends query and returns a request handle plus runtime event stream
  - `await/1,2` - Awaits a specific request's completion
  - `ask_sync/2,3` - Sync convenience: sends query and waits for result
  - `on_before_cmd/2` - Captures request in state before processing
  - `on_after_cmd/3` - Updates request result when done

  ## Request Tracking

  Each `ask/2` call returns a `Request` struct that can be awaited:

      {:ok, request} = MyAgent.ask(pid, "What is 2+2?")
      {:ok, result} = MyAgent.await(request, timeout: 30_000)

  Or use the synchronous convenience wrapper:

      {:ok, result} = MyAgent.ask_sync(pid, "What is 2+2?", timeout: 30_000)

  This pattern follows Elixir's `Task.async/await` idiom and enables safe
  concurrent request handling.

  ## State Fields

  The agent state includes:

  - `:model` - The LLM model being used
  - `:requests` - Map of request_id => request state (for concurrent tracking)
  - `:last_request_id` - ID of the most recent request
  - `:last_query` - The most recent query (backward compat)
  - `:last_answer` - The final answer from the last completed query (backward compat)
  - `:completed` - Boolean indicating if the last query is complete (backward compat)

  ## Task Supervisor

  Each agent instance gets its own Task.Supervisor automatically started via the
  `Jido.AI.Plugins.TaskSupervisor`. This supervisor is used for:
  - LLM streaming operations
  - Tool execution
  - Other async operations within the agent's lifecycle

  The supervisor is stored in the skill's internal state (`agent.state.__task_supervisor_skill__`)
  and is accessible via `Jido.AI.Directive.Helpers.get_task_supervisor/1`. It is automatically
  cleaned up when the agent terminates.

  ## Example

      {:ok, pid} = Jido.AgentServer.start(agent: MyApp.WeatherAgent)

      # Async pattern (preferred for concurrent requests)
      {:ok, request} = MyApp.WeatherAgent.ask(pid, "What's the weather in Tokyo?")
      {:ok, answer} = MyApp.WeatherAgent.await(request)

      # Sync pattern (convenience for simple cases)
      {:ok, answer} = MyApp.WeatherAgent.ask_sync(pid, "What's the weather in Tokyo?")

  ## Per-Request Tool Context

  You can pass per-request context that will be merged with the agent's base tool_context:

      {:ok, request} = MyApp.WeatherAgent.ask(pid, "Get my preferences",
        tool_context: %{actor: current_user, tenant_id: "acme"})

  ReAct and ToT tool execution contexts include a runtime-managed snapshot at `:state`
  (canonical, core-aligned). This key is reserved and overrides same-named values
  from `tool_context`.
  """

  @default_model :fast
  @default_max_iterations 10
  @default_max_tokens 4_096

  @doc false
  def expand_aliases_in_ast(ast, caller_env) do
    Macro.prewalk(ast, fn
      {:__aliases__, _, _} = alias_node ->
        Macro.expand(alias_node, caller_env)

      # Allow literals
      literal when is_atom(literal) or is_binary(literal) or is_number(literal) ->
        literal

      # Allow list syntax
      list when is_list(list) ->
        list

      # Allow map struct syntax: %{...}
      {:%{}, meta, pairs} ->
        {:%{}, meta, pairs}

      # Allow struct syntax: %Module{...}
      {:%, meta, args} ->
        {:%, meta, args}

      # Allow tuple syntax: {...}
      {:{}, meta, elements} ->
        {:{}, meta, elements}

      # Allow 2-tuples (key-value pairs in maps)
      {key, value} when not is_atom(key) or key not in [:__aliases__, :%, :%{}, :{}] ->
        {key, value}

      # Reject function calls and other unsafe constructs
      {func, meta, args} = node when is_atom(func) and is_list(args) ->
        if func in [:__aliases__, :%, :%{}, :{}] do
          node
        else
          raise CompileError,
            description:
              "Unsafe construct in tool_context or tools: function call #{inspect(func)} is not allowed. " <>
                "Only module aliases, atoms, strings, numbers, lists, and maps are permitted.",
            line: Keyword.get(meta, :line, 0)
        end

      # Reject module attributes with clear error
      {:@, meta, [{name, _, _}]} ->
        raise CompileError,
          description:
            "Module attributes (@#{name}) are not supported in tool_context, tools, or specialists. " <>
              "Define the value inline or use a compile-time constant.",
          line: Keyword.get(meta, :line, 0)

      # Reject pinned variables
      {:^, meta, _} ->
        raise CompileError,
          description:
            "Pinned variables (^) are not supported in tool_context, tools, or specialists. " <>
              "Use literal values instead.",
          line: Keyword.get(meta, :line, 0)

      other ->
        other
    end)
  end

  @doc false
  def expand_and_eval_literal_option(value, caller_env) do
    case value do
      nil ->
        nil

      value when is_tuple(value) ->
        value
        |> expand_aliases_in_ast(caller_env)
        |> Code.eval_quoted([], caller_env)
        |> elem(0)

      value when is_map(value) ->
        value

      value when is_list(value) ->
        value
        |> expand_aliases_in_ast(caller_env)
        |> Code.eval_quoted([], caller_env)
        |> elem(0)

      {:%{}, _, _} = map_ast ->
        map_ast
        |> expand_aliases_in_ast(caller_env)
        |> Code.eval_quoted([], caller_env)
        |> elem(0)

      {:%, _, _} = struct_ast ->
        struct_ast
        |> expand_aliases_in_ast(caller_env)
        |> Code.eval_quoted([], caller_env)
        |> elem(0)

      other ->
        other
    end
  end

  @doc false
  def normalize_system_prompt_value(value, file, line) do
    case value do
      nil -> :absent
      false -> :absent
      "" -> :absent
      value when is_binary(value) -> {:resolved, value}
      other -> raise_invalid_system_prompt!(other, file, line)
    end
  end

  defp raise_invalid_system_prompt!(value, file, line) do
    raise CompileError,
      description:
        "system_prompt must be a binary, nil, false, or a compile-time literal/module attribute resolving to one, got: #{inspect(value)}",
      file: file,
      line: line
  end

  defp system_prompt_line({_, meta, _}, default), do: Keyword.get(meta, :line, default)
  defp system_prompt_line(_, default), do: default

  @doc false
  def expand_and_eval_output_option(nil, _caller_env, _file, _line), do: nil

  def expand_and_eval_output_option(value, caller_env, file, line) do
    value =
      case value do
        value when is_map(value) ->
          value

        value when is_list(value) ->
          value
          |> Macro.prewalk(fn
            {:__aliases__, _, _} = alias_node -> Macro.expand(alias_node, caller_env)
            other -> other
          end)
          |> Code.eval_quoted([], caller_env)
          |> elem(0)

        other ->
          other
          |> Macro.expand(caller_env)
          |> Code.eval_quoted([], caller_env)
          |> elem(0)
      end

    Jido.AI.Output.new!(value)
  rescue
    error ->
      raise CompileError,
        description: "invalid output option: #{Exception.message(error)}",
        file: file,
        line: line
  end

  defmacro __using__(opts) do
    # Extract all values at compile time (in the calling module's context)
    name = Keyword.fetch!(opts, :name)
    tools_ast = Keyword.fetch!(opts, :tools)

    # Expand module aliases in the tools list to actual module atoms
    # This handles {:__aliases__, _, [...]} tuples from macro expansion
    tools =
      Enum.map(tools_ast, fn
        {:__aliases__, _, _} = alias_ast -> Macro.expand(alias_ast, __CALLER__)
        mod when is_atom(mod) -> mod
      end)

    description = Keyword.get(opts, :description, "AI agent #{name}")
    tags = Keyword.get(opts, :tags, [])
    system_prompt_raw = Keyword.get(opts, :system_prompt)
    system_prompt_line = system_prompt_line(system_prompt_raw, __CALLER__.line)

    system_prompt =
      case system_prompt_raw do
        {:@, _, [{_name, _, _}]} = attr_ast ->
          {:deferred, attr_ast}

        other ->
          expanded = Macro.expand(other, __CALLER__)

          if Macro.quoted_literal?(expanded) do
            {resolved, _binding} = Code.eval_quoted(expanded, [], __CALLER__)
            __MODULE__.normalize_system_prompt_value(resolved, __CALLER__.file, system_prompt_line)
          else
            raise CompileError,
              description:
                "system_prompt only supports binaries, nil, false, compile-time literal expressions, or bare module attributes",
              file: __CALLER__.file,
              line: system_prompt_line
          end
      end

    model =
      opts
      |> Keyword.get(:model, @default_model)
      |> __MODULE__.expand_and_eval_literal_option(__CALLER__)

    max_iterations = Keyword.get(opts, :max_iterations, @default_max_iterations)
    max_tokens = Keyword.get(opts, :max_tokens, @default_max_tokens)
    streaming = Keyword.get(opts, :streaming, true)
    request_policy = Keyword.get(opts, :request_policy, :reject)
    tool_timeout_ms = Keyword.get(opts, :tool_timeout_ms, 15_000)
    tool_max_retries = Keyword.get(opts, :tool_max_retries, 1)
    tool_retry_backoff_ms = Keyword.get(opts, :tool_retry_backoff_ms, 200)
    stream_timeout_ms = Keyword.get(opts, :stream_timeout_ms, Keyword.get(opts, :stream_receive_timeout_ms, 0))
    # ReAct delegation is always enabled; keep runtime_adapter option for compatibility only.
    _runtime_adapter_opt = Keyword.get(opts, :runtime_adapter, true)
    runtime_adapter = true
    runtime_task_supervisor = Keyword.get(opts, :runtime_task_supervisor)
    observability = Keyword.get(opts, :observability, %{})

    req_http_options =
      opts
      |> Keyword.get(:req_http_options, [])
      |> __MODULE__.expand_and_eval_literal_option(__CALLER__)

    llm_opts =
      opts
      |> Keyword.get(:llm_opts, [])
      |> __MODULE__.expand_and_eval_literal_option(__CALLER__)

    output =
      opts
      |> Keyword.get(:output)
      |> __MODULE__.expand_and_eval_output_option(__CALLER__, __CALLER__.file, __CALLER__.line)

    request_transformer =
      case Keyword.get(opts, :request_transformer) do
        {:__aliases__, _, _} = alias_ast -> Macro.expand(alias_ast, __CALLER__)
        other -> other
      end

    agent_effect_policy =
      opts
      |> Keyword.get(:effect_policy, %{})
      |> __MODULE__.expand_and_eval_literal_option(__CALLER__)

    strategy_effect_policy =
      opts
      |> Keyword.get(:strategy_effect_policy, %{})
      |> __MODULE__.expand_and_eval_literal_option(__CALLER__)

    # Don't extract tool_context here - it contains AST with module aliases
    # that need to be evaluated in the calling module's context
    plugins = Keyword.get(opts, :plugins, [])

    default_plugins =
      opts
      |> Keyword.get(:default_plugins)
      |> __MODULE__.expand_and_eval_literal_option(__CALLER__)

    ai_plugins = Jido.AI.PluginStack.default_plugins(opts)

    # Extract tool_context at macro expansion time
    # Use safe alias-only expansion instead of Code.eval_quoted
    tool_context =
      case Keyword.get(opts, :tool_context) do
        nil ->
          %{}

        {:%, _, _} = map_ast ->
          # It's a struct/map AST - expand aliases safely and evaluate
          expanded_ast = __MODULE__.expand_aliases_in_ast(map_ast, __CALLER__)
          {evaluated, _} = Code.eval_quoted(expanded_ast, [], __CALLER__)
          evaluated

        {:%{}, _, _} = map_ast ->
          # Plain map AST - expand aliases safely and evaluate
          expanded_ast = __MODULE__.expand_aliases_in_ast(map_ast, __CALLER__)
          {evaluated, _} = Code.eval_quoted(expanded_ast, [], __CALLER__)
          evaluated

        other when is_map(other) ->
          other
      end

    strategy_opts =
      [
        tools: tools,
        model: model,
        streaming: streaming,
        max_iterations: max_iterations,
        max_tokens: max_tokens,
        request_policy: request_policy,
        tool_timeout_ms: tool_timeout_ms,
        tool_max_retries: tool_max_retries,
        tool_retry_backoff_ms: tool_retry_backoff_ms,
        stream_timeout_ms: stream_timeout_ms,
        runtime_adapter: runtime_adapter,
        runtime_task_supervisor: runtime_task_supervisor,
        observability: observability,
        req_http_options: req_http_options,
        llm_opts: llm_opts,
        output: output,
        request_transformer: request_transformer,
        agent_effect_policy: agent_effect_policy,
        strategy_effect_policy: strategy_effect_policy,
        tool_context: tool_context
      ]
      |> then(fn o ->
        case system_prompt do
          :absent -> o
          {:resolved, value} -> Keyword.put(o, :system_prompt, value)
          {:deferred, _attr_ast} -> o
        end
      end)

    strategy_opts_ast =
      case system_prompt do
        {:deferred, attr_ast} ->
          quote do
            case unquote(__MODULE__).normalize_system_prompt_value(
                   unquote(attr_ast),
                   __ENV__.file,
                   unquote(system_prompt_line)
                 ) do
              :absent ->
                unquote(Macro.escape(strategy_opts))

              {:resolved, value} ->
                Keyword.put(unquote(Macro.escape(strategy_opts)), :system_prompt, value)
            end
          end

        _ ->
          Macro.escape(strategy_opts)
      end

    # Build base_schema AST at macro expansion time
    # Includes request tracking fields for concurrent request isolation
    base_schema_ast =
      quote do
        Zoi.object(%{
          __strategy__: Zoi.map() |> Zoi.default(%{}),
          model: Zoi.any() |> Zoi.default(unquote(Macro.escape(model))),
          # Request tracking for concurrent request isolation
          requests: Zoi.map() |> Zoi.default(%{}),
          last_request_id: Zoi.string() |> Zoi.optional(),
          # Backward compatibility fields (convenience pointers to most recent)
          last_query: Zoi.string() |> Zoi.default(""),
          last_answer: Zoi.string() |> Zoi.default(""),
          completed: Zoi.boolean() |> Zoi.default(false)
        })
      end

    quote location: :keep do
      use Jido.Agent,
        name: unquote(name),
        description: unquote(description),
        tags: unquote(tags),
        plugins: unquote(ai_plugins) ++ unquote(plugins),
        default_plugins: unquote(Macro.escape(default_plugins)),
        strategy: {Jido.AI.Reasoning.ReAct.Strategy, unquote(strategy_opts_ast)},
        schema: unquote(base_schema_ast)

      unquote(__MODULE__.compatibility_overrides_ast())

      import Jido.AI.Agent, only: [tools_from_skills: 1]

      alias Jido.AI.{Request, Signal}

      @doc """
      Send a query to the agent asynchronously.

      Returns `{:ok, %Request{}}` immediately. Use `await/2` to wait for the result.

      ## Options

      - `:tool_context` - Additional context map merged with agent's tool_context
      - `:tools` - Request-scoped tool registry override for this run only
      - `:allowed_tools` - Request-scoped allowlist of tool names
      - `:request_transformer` - Module implementing per-turn ReAct request shaping
      - `:stream_timeout_ms` - Request-scoped runtime inactivity timeout.
        `:stream_receive_timeout_ms` is accepted as a compatibility alias.
      - `:req_http_options` - Per-request Req HTTP options forwarded to ReAct runtime
      - `:llm_opts` - Per-request ReqLLM generation options forwarded to ReAct runtime
      - `:output` - `:raw` to bypass structured output or a request-scoped output config
      - `:stream_to` - Optional request-scoped runtime event sink, currently `{:pid, pid}`
      - `:timeout` - Timeout for the underlying cast (default: no timeout)

      ## Examples

          {:ok, request} = MyAgent.ask(pid, "What is 2+2?")
          {:ok, result} = MyAgent.await(request)

      """
      @spec ask(pid() | atom() | {:via, module(), term()}, String.t(), keyword()) ::
              {:ok, Request.Handle.t()} | {:error, term()}
      def ask(pid, query, opts \\ []) when is_binary(query) do
        Request.create_and_send(
          pid,
          query,
          Keyword.merge(opts,
            signal_type: "ai.react.query",
            source: "/ai/react/agent"
          )
        )
      end

      @doc """
      Send a query and return both its request handle and runtime event stream.

      The returned enumerable yields canonical ReAct runtime events until the
      request emits `:request_completed`, `:request_failed`, or
      `:request_cancelled`.

      ## Options

      Accepts the same options as `ask/3`, plus:
      - `:stream_event_timeout_ms` - Optional mailbox receive timeout for the enumerable

      ## Examples

          {:ok, %{request: request, events: events}} = MyAgent.ask_stream(pid, "What is 2+2?")
          for event <- events do
            IO.inspect(event.kind)
          end
          {:ok, result} = MyAgent.await(request)

      """
      @spec ask_stream(pid() | atom() | {:via, module(), term()}, String.t(), keyword()) ::
              {:ok, %{request: Request.Handle.t(), events: Enumerable.t()}} | {:error, term()}
      def ask_stream(pid, query, opts \\ []) when is_binary(query) do
        opts = Keyword.put(opts, :stream_to, {:pid, self()})

        with {:ok, request} <- ask(pid, query, opts) do
          {:ok, %{request: request, events: Request.Stream.events(request, opts)}}
        end
      end

      @doc """
      Await the result of a specific request.

      Blocks until the request completes, fails, or times out.

      ## Options

      - `:timeout` - How long to wait in milliseconds (default: 30_000)

      ## Returns

      - `{:ok, result}` - Request completed successfully
      - `{:error, :timeout}` - Request didn't complete in time
      - `{:error, reason}` - Request failed

      ## Examples

          {:ok, request} = MyAgent.ask(pid, "What is 2+2?")
          {:ok, "4"} = MyAgent.await(request, timeout: 10_000)

      """
      @spec await(Request.Handle.t(), keyword()) :: {:ok, any()} | {:error, term()}
      def await(request, opts \\ []) do
        Request.await(request, opts)
      end

      @doc """
      Send a query and wait for the result synchronously.

      Convenience wrapper that combines `ask/3` and `await/2`.

      ## Options

      - `:tool_context` - Additional context map merged with agent's tool_context
      - `:tools` - Request-scoped tool registry override for this run only
      - `:allowed_tools` - Request-scoped allowlist of tool names
      - `:request_transformer` - Module implementing per-turn ReAct request shaping
      - `:stream_timeout_ms` - Request-scoped runtime inactivity timeout.
        `:stream_receive_timeout_ms` is accepted as a compatibility alias.
      - `:req_http_options` - Per-request Req HTTP options forwarded to ReAct runtime
      - `:llm_opts` - Per-request ReqLLM generation options forwarded to ReAct runtime
      - `:output` - `:raw` to bypass structured output or a request-scoped output config
      - `:timeout` - How long to wait in milliseconds (default: 30_000)

      ## Examples

          {:ok, result} = MyAgent.ask_sync(pid, "What is 2+2?", timeout: 10_000)

      """
      @spec ask_sync(pid() | atom() | {:via, module(), term()}, String.t(), keyword()) ::
              {:ok, any()} | {:error, term()}
      def ask_sync(pid, query, opts \\ []) when is_binary(query) do
        Request.send_and_await(
          pid,
          query,
          Keyword.merge(opts,
            signal_type: "ai.react.query",
            source: "/ai/react/agent"
          )
        )
      end

      @impl true
      def on_before_cmd(agent, {:ai_react_start, %{query: query} = params} = action) do
        # Ensure we have a request_id for tracking
        {request_id, params} = Request.ensure_request_id(params)
        action = {:ai_react_start, params}

        # Use RequestTracking to manage state
        agent = Request.start_request(agent, request_id, query, stream_to: params[:stream_to])

        {:ok, agent, action}
      end

      @impl true
      def on_before_cmd(
            agent,
            {:ai_react_request_error, %{request_id: request_id, reason: reason, message: message}} = action
          ) do
        error = {:rejected, reason, message}
        stream_to = get_in(agent.state, [:requests, request_id, :stream_to])

        agent = Request.fail_request(agent, request_id, error)
        Request.Stream.send_event(stream_to, Request.Stream.failed_event(request_id, error, reason: reason))
        emit_request_failed_signal(agent, request_id, error)
        {:ok, agent, action}
      end

      @impl true
      def on_before_cmd(agent, {:ai_react_cancel, params}) do
        request_id = params[:request_id] || agent.state[:last_request_id]
        action = {:ai_react_cancel, Map.put(params, :request_id, request_id)}
        {:ok, agent, action}
      end

      @impl true
      def on_before_cmd(agent, action), do: {:ok, agent, action}

      @impl true
      def on_after_cmd(agent, {:ai_react_start, %{request_id: request_id}}, directives) do
        snap = strategy_snapshot(agent)
        should_finalize? = request_pending?(agent, request_id) and snap.done?

        agent =
          if should_finalize? do
            case snap.status do
              :success ->
                agent =
                  Request.complete_request_from_snapshot(agent, request_id, snap)

                emit_request_completed_signal(agent, request_id, snap.result)
                agent

              :failure ->
                reason = failure_reason(snap)
                agent = Request.fail_request(agent, request_id, reason)
                emit_request_failed_signal(agent, request_id, reason)
                agent

              _ ->
                agent
            end
          else
            agent
          end

        {:ok, agent, directives}
      end

      @impl true
      def on_after_cmd(agent, {:ai_react_cancel, %{request_id: request_id, reason: reason}}, directives) do
        agent =
          if is_binary(request_id) and request_pending?(agent, request_id) do
            failure = {:cancelled, reason}
            emit_request_failed_signal(agent, request_id, failure)
            Request.fail_request(agent, request_id, failure)
          else
            agent
          end

        {:ok, agent, directives}
      end

      @impl true
      def on_after_cmd(agent, {:ai_react_request_error, _params}, directives) do
        {:ok, agent, directives}
      end

      @impl true
      def on_after_cmd(agent, _action, directives) do
        snap = strategy_snapshot(agent)
        request_id = agent.state[:last_request_id]
        should_finalize? = is_binary(request_id) and request_pending?(agent, request_id) and snap.done?

        agent =
          if should_finalize? do
            agent = %{
              agent
              | state:
                  Map.merge(agent.state, %{
                    last_answer: Request.compat_text(snap.result),
                    completed: true
                  })
            }

            case snap.status do
              :success ->
                Request.complete_request_from_snapshot(agent, request_id, snap)

              :failure ->
                reason = failure_reason(snap)
                Request.fail_request(agent, request_id, reason)

              _ ->
                agent
            end
          else
            agent
          end

        {:ok, agent, directives}
      end

      defp request_pending?(agent, request_id) when is_binary(request_id) do
        case Request.get_request(agent, request_id) do
          %{status: :pending} -> true
          _ -> false
        end
      end

      defp request_pending?(_agent, _request_id), do: false

      defp failure_reason(snap) do
        details = snap.details

        case details[:termination_reason] do
          :cancelled ->
            {:cancelled, details[:cancel_reason] || :cancelled}

          reason when not is_nil(reason) ->
            {:failed, reason, snap.result}

          _ ->
            {:failed, :unknown, snap.result}
        end
      end

      defp emit_request_completed_signal(agent, request_id, result) do
        if lifecycle_signals_enabled?(agent) do
          signal =
            Signal.RequestCompleted.new!(%{
              request_id: request_id,
              result: result,
              run_id: request_id
            })

          Jido.AgentServer.cast(self(), signal)
        end
      rescue
        _ -> :ok
      end

      defp emit_request_failed_signal(agent, request_id, error) do
        if lifecycle_signals_enabled?(agent) do
          signal =
            Signal.RequestFailed.new!(%{
              request_id: request_id,
              error: error,
              run_id: request_id
            })

          Jido.AgentServer.cast(self(), signal)
        end
      rescue
        _ -> :ok
      end

      defp lifecycle_signals_enabled?(agent) do
        get_in(agent.state, [:__strategy__, :config, :observability, :emit_lifecycle_signals?]) != false
      end

      @doc """
      Cancel an in-flight request.

      Sends a cancellation signal to the agent. Note that this is advisory -
      the underlying LLM request may still complete.

      ## Options

      - `:reason` - Reason for cancellation (default: :user_cancelled)

      ## Examples

          {:ok, request} = MyAgent.ask(pid, "What is 2+2?")
          :ok = MyAgent.cancel(pid)

      """
      @spec cancel(pid() | atom() | {:via, module(), term()}, keyword()) :: :ok | {:error, term()}
      def cancel(pid, opts \\ []) do
        reason = Keyword.get(opts, :reason, :user_cancelled)
        request_id = Keyword.get(opts, :request_id)

        payload =
          %{reason: reason}
          |> then(fn p ->
            if is_binary(request_id), do: Map.put(p, :request_id, request_id), else: p
          end)

        signal = Jido.Signal.new!("ai.react.cancel", payload, source: "/ai/react/agent")
        Jido.AgentServer.cast(pid, signal)
      end

      @doc """
      Steer an active request with additional user-visible input.

      Returns `{:ok, agent}` when the input is queued for the current ReAct run
      or `{:error, {:rejected, reason}}` when no eligible run is active.

      Queued input is best-effort. If the run terminates before the runtime
      drains the queue into conversation state, the queued input is dropped.
      """
      @spec steer(pid() | atom() | {:via, module(), term()}, String.t(), keyword()) ::
              {:ok, Jido.Agent.t()} | {:error, term()}
      def steer(pid, content, opts \\ []) when is_binary(content) do
        Jido.AI.Reasoning.ReAct.steer(pid, content, Keyword.put_new(opts, :source, "/ai/react/agent"))
      end

      @doc """
      Inject user-visible input into an active request.

      This is intended for programmatic or inter-agent steering and follows the
      same queuing rules as `steer/3`.
      """
      @spec inject(pid() | atom() | {:via, module(), term()}, String.t(), keyword()) ::
              {:ok, Jido.Agent.t()} | {:error, term()}
      def inject(pid, content, opts \\ []) when is_binary(content) do
        Jido.AI.Reasoning.ReAct.inject(pid, content, Keyword.put_new(opts, :source, "/ai/react/agent"))
      end

      defoverridable on_before_cmd: 2,
                     on_after_cmd: 3,
                     ask: 3,
                     ask_stream: 3,
                     await: 2,
                     ask_sync: 3,
                     cancel: 2,
                     steer: 3,
                     inject: 3
    end
  end

  @doc false
  @spec compatibility_overrides_ast() :: Macro.t()
  def compatibility_overrides_ast do
    quote location: :keep do
      # Broaden the contract to avoid false positives from upstream plugin-spec typing.
      @spec plugin_specs() :: [map()]
      def plugin_specs, do: @plugin_specs

      @impl true
      @spec restore(map(), map()) :: {:ok, Jido.Agent.t()} | {:error, term()}
      def restore(data, ctx) when is_map(data) and is_map(ctx) do
        agent = new(id: data[:id] || data["id"])
        base_state = data[:state] || data["state"] || %{}
        agent = %{agent | state: Map.merge(agent.state, base_state)}
        externalized_keys = data[:externalized_keys] || %{}

        Enum.reduce_while(@plugin_instances, {:ok, agent}, fn instance, {:ok, acc} ->
          config = instance.config || %{}
          restore_ctx = Map.put(ctx, :config, config)

          ext_key =
            Enum.find_value(externalized_keys, fn {k, v} ->
              if v == instance.state_key, do: k
            end)

          pointer = if is_nil(ext_key), do: nil, else: Map.get(data, ext_key)

          if pointer do
            case instance.module.on_restore(pointer, restore_ctx) do
              {:ok, nil} ->
                {:cont, {:ok, acc}}

              {:ok, restored_state} ->
                {:cont, {:ok, %{acc | state: Map.put(acc.state, instance.state_key, restored_state)}}}

              {:error, reason} ->
                {:halt, {:error, reason}}
            end
          else
            {:cont, {:ok, acc}}
          end
        end)
      end

      def restore(_data, _ctx), do: {:error, :invalid_checkpoint_payload}
    end
  end

  @doc """
  Extract tool action modules from skills.

  Useful when you want to use skill actions as agent tools.

  ## Example

      @skills [MyApp.WeatherSkill, MyApp.LocationSkill]

      use Jido.AI.Agent,
        name: "weather_agent",
        tools: Jido.AI.Agent.tools_from_skills(@skills),
        skills: Enum.map(@skills, & &1.skill_spec(%{}))
  """
  @spec tools_from_skills([module()]) :: [module()]
  def tools_from_skills(skill_modules) when is_list(skill_modules) do
    skill_modules
    |> Enum.flat_map(& &1.actions())
    |> Enum.uniq()
  end
end
