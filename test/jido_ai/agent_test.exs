defmodule Jido.AI.AgentTest do
  @moduledoc """
  Tests for Jido.AI.Agent macro and compile-time alias expansion.
  """
  use ExUnit.Case, async: true
  import ExUnit.CaptureIO

  alias Jido.Agent.Strategy.State, as: StratState
  alias Jido.AI.Context, as: AIContext
  alias Jido.AI.Request
  alias Jido.AI.Agent
  alias Jido.AI.Reasoning.ReAct.Event
  alias Jido.AI.Reasoning.ReAct.Strategy, as: ReAct

  # ============================================================================
  # Test Action Modules (simulating external modules like ash_jido)
  # ============================================================================

  defmodule TestDomain do
    @moduledoc "Mock domain module for testing tool_context resolution"
    def name, do: "test_domain"
  end

  defmodule TestActor do
    @moduledoc "Mock actor module for testing tool_context resolution"
    def name, do: "test_actor"
  end

  defmodule TestCalculator do
    use Jido.Action,
      name: "calculator",
      description: "A simple calculator"

    def run(%{operation: "add", a: a, b: b}, _ctx), do: {:ok, %{result: a + b}}
    def run(%{operation: "multiply", a: a, b: b}, _ctx), do: {:ok, %{result: a * b}}
  end

  defmodule TestSearch do
    use Jido.Action,
      name: "search",
      description: "Search for information"

    def run(%{query: query}, _ctx), do: {:ok, %{results: ["Found: #{query}"]}}
  end

  defmodule TestRequestTransformer do
    def transform_request(request, _state, _config, _context), do: {:ok, request}
  end

  defmodule ReplacementMemoryPlugin do
    @moduledoc false
    @state_schema Zoi.object(%{namespace: Zoi.string() |> Zoi.default("agent:replacement")})
    @config_schema Zoi.object(%{namespace: Zoi.string() |> Zoi.default("agent:replacement")})

    use Jido.Plugin,
      name: "replacement_memory",
      state_key: :__memory__,
      actions: [],
      schema: @state_schema,
      config_schema: @config_schema,
      singleton: true,
      capabilities: [:memory]

    @impl true
    def mount(_agent, config) do
      {:ok, %{namespace: Map.get(config, :namespace, "agent:replacement")}}
    end
  end

  # ============================================================================
  # Test Agents Using Agent Macro
  # ============================================================================

  defmodule BasicAgent do
    use Jido.AI.Agent,
      name: "basic_agent",
      description: "A basic test agent",
      tools: [TestCalculator, TestSearch]
  end

  defmodule AgentWithToolContext do
    use Jido.AI.Agent,
      name: "agent_with_context",
      tools: [TestCalculator],
      tool_context: %{
        domain: TestDomain,
        actor: TestActor,
        static_value: "hello"
      }
  end

  defmodule AgentWithPlainMapContext do
    use Jido.AI.Agent,
      name: "agent_with_plain_map",
      tools: [TestCalculator],
      tool_context: %{tenant_id: "tenant_123", enabled: true}
  end

  defmodule AgentWithLlmOpts do
    use Jido.AI.Agent,
      name: "agent_with_llm_opts",
      tools: [TestCalculator],
      llm_opts: [thinking: %{type: :enabled, budget_tokens: 800}, reasoning_effort: :high],
      req_http_options: [adapter: [recv_timeout: 2_000]]
  end

  defmodule AgentWithStreamingDisabled do
    use Jido.AI.Agent,
      name: "agent_no_streaming",
      tools: [TestCalculator],
      streaming: false
  end

  defmodule AgentWithMaxTokens do
    use Jido.AI.Agent,
      name: "agent_with_max_tokens",
      tools: [TestCalculator],
      max_tokens: 4_096
  end

  defmodule AgentWithStreamTimeout do
    use Jido.AI.Agent,
      name: "agent_with_stream_timeout",
      tools: [TestCalculator],
      stream_timeout_ms: 123_456
  end

  defmodule AgentWithRequestTransformer do
    use Jido.AI.Agent,
      name: "agent_with_request_transformer",
      tools: [TestCalculator],
      request_transformer: TestRequestTransformer
  end

  defmodule AgentWithInlineModelMap do
    use Jido.AI.Agent,
      name: "agent_with_inline_model_map",
      tools: [TestCalculator],
      model: %{provider: :openai, id: "gpt-4o-mini", base_url: "http://localhost:4000/v1"}
  end

  defmodule AgentWithTupleModelSpec do
    use Jido.AI.Agent,
      name: "agent_with_tuple_model_spec",
      tools: [TestCalculator],
      model: {:openai, "gpt-4o-mini", []}
  end

  defmodule AgentWithStreamTimeoutAlias do
    use Jido.AI.Agent,
      name: "agent_with_stream_timeout_alias",
      tools: [TestCalculator],
      stream_timeout_ms: 45_000
  end

  defmodule AgentWithModuleAttrSystemPrompt do
    @my_prompt "You are a helpful testing assistant."

    use Jido.AI.Agent,
      name: "agent_with_attr_prompt",
      tools: [TestCalculator],
      system_prompt: @my_prompt
  end

  defmodule AgentWithFalseSystemPrompt do
    use Jido.AI.Agent,
      name: "agent_with_false_prompt",
      tools: [TestCalculator],
      system_prompt: false
  end

  defmodule AgentWithNilSystemPrompt do
    use Jido.AI.Agent,
      name: "agent_with_nil_prompt",
      tools: [TestCalculator],
      system_prompt: nil
  end

  defmodule AgentWithoutDefaultMemory do
    use Jido.AI.Agent,
      name: "agent_without_default_memory",
      tools: [TestCalculator],
      default_plugins: %{__memory__: false}
  end

  defmodule AgentWithReplacementMemory do
    use Jido.AI.Agent,
      name: "agent_with_replacement_memory",
      tools: [TestCalculator],
      default_plugins: %{__memory__: {ReplacementMemoryPlugin, %{namespace: "agent:ai-replacement"}}}
  end

  # ============================================================================
  # expand_aliases_in_ast/2 Tests
  # ============================================================================

  describe "expand_aliases_in_ast/2" do
    test "expands module aliases to atoms" do
      # Simulate AST for %{domain: TestDomain}
      ast = {:%{}, [], [domain: {:__aliases__, [alias: false], [:SomeModule]}]}

      # Create a mock caller env
      env = __ENV__

      # The function should walk the AST and expand aliases
      result = Agent.expand_aliases_in_ast(ast, env)

      # The __aliases__ node should be expanded (in this case to SomeModule atom)
      assert is_tuple(result)
    end

    test "allows literal values unchanged" do
      ast = {:%{}, [], [key: "string", num: 42, flag: true, atom_val: :test]}
      env = __ENV__

      result = Agent.expand_aliases_in_ast(ast, env)

      # Should preserve the structure
      assert is_tuple(result)
    end

    test "allows nested maps" do
      ast = {:%{}, [], [outer: {:%{}, [], [inner: "value"]}]}
      env = __ENV__

      result = Agent.expand_aliases_in_ast(ast, env)

      assert is_tuple(result)
    end

    test "allows lists" do
      ast = {:%{}, [], [items: [1, 2, 3]]}
      env = __ENV__

      result = Agent.expand_aliases_in_ast(ast, env)

      assert is_tuple(result)
    end

    test "raises CompileError for function calls" do
      # Simulate AST for %{value: some_function()}
      ast = {:%{}, [], [value: {:some_function, [line: 1], []}]}
      env = __ENV__

      assert_raise CompileError, ~r/Unsafe construct.*function call/, fn ->
        Agent.expand_aliases_in_ast(ast, env)
      end
    end
  end

  # ============================================================================
  # Agent Macro Compilation Tests
  # ============================================================================

  describe "Agent macro" do
    test "compiles agent with basic options" do
      assert function_exported?(BasicAgent, :ask, 2)
      assert function_exported?(BasicAgent, :ask, 3)
      assert function_exported?(BasicAgent, :ask_stream, 3)
      assert function_exported?(BasicAgent, :steer, 3)
      assert function_exported?(BasicAgent, :inject, 3)
      assert function_exported?(BasicAgent, :on_before_cmd, 2)
      assert function_exported?(BasicAgent, :on_after_cmd, 3)
    end

    test "agent has correct name" do
      agent = BasicAgent.new()
      assert agent.name == "basic_agent"
    end

    test "agent has correct description" do
      agent = BasicAgent.new()
      assert agent.description == "A basic test agent"
    end

    test "forwards default plugin exclusions to Jido.Agent" do
      modules = Enum.map(AgentWithoutDefaultMemory.plugin_instances(), & &1.module)

      refute Jido.Memory.Plugin in modules
      assert Jido.Thread.Plugin in modules
      assert Jido.Identity.Plugin in modules
    end

    test "forwards default plugin replacements with config to Jido.Agent" do
      modules = Enum.map(AgentWithReplacementMemory.plugin_instances(), & &1.module)
      agent = AgentWithReplacementMemory.new()

      assert ReplacementMemoryPlugin in modules
      refute Jido.Memory.Plugin in modules
      assert agent.state[:__memory__].namespace == "agent:ai-replacement"
    end

    test "tool_context with module aliases resolves correctly" do
      # When using Agent, the strategy is auto-initialized via new()
      # The config is stored in agent.state.__strategy__.config
      agent = AgentWithToolContext.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      # Modules should be resolved to atoms, not AST tuples
      # Now stored as base_tool_context (persistent)
      assert config.base_tool_context[:domain] == TestDomain
      assert config.base_tool_context[:actor] == TestActor
      assert config.base_tool_context[:static_value] == "hello"
    end

    test "tool_context with plain map values works" do
      agent = AgentWithPlainMapContext.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      # Now stored as base_tool_context (persistent)
      assert config.base_tool_context[:tenant_id] == "tenant_123"
      assert config.base_tool_context[:enabled] == true
    end

    test "llm_opts and req_http_options are forwarded into strategy config" do
      agent = AgentWithLlmOpts.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.base_llm_opts == [thinking: %{type: :enabled, budget_tokens: 800}, reasoning_effort: :high]
      assert config.base_req_http_options == [adapter: [recv_timeout: 2_000]]
    end

    test "streaming: false is forwarded into strategy config" do
      agent = AgentWithStreamingDisabled.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.streaming == false
    end

    test "max_tokens is forwarded into strategy config" do
      agent = AgentWithMaxTokens.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.max_tokens == 4_096
    end

    test "stream_timeout_ms is forwarded into strategy config" do
      agent = AgentWithStreamTimeout.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.stream_timeout_ms == 123_456
    end

    test "request_transformer is forwarded into strategy config" do
      agent = AgentWithRequestTransformer.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.request_transformer == TestRequestTransformer
    end

    test "inline map model specs are evaluated and forwarded into strategy config" do
      agent = AgentWithInlineModelMap.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.model == %{provider: :openai, id: "gpt-4o-mini", base_url: "http://localhost:4000/v1"}
    end

    test "tuple model specs are evaluated and forwarded into strategy config" do
      agent = AgentWithTupleModelSpec.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.model == {:openai, "gpt-4o-mini", []}
    end

    test "stream_timeout_ms alias is forwarded into strategy config" do
      agent = AgentWithStreamTimeoutAlias.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.stream_timeout_ms == 45_000
      assert config.stream_receive_timeout_ms == 45_000
    end

    test "system_prompt from module attribute is resolved at compile time" do
      agent = AgentWithModuleAttrSystemPrompt.new()
      state = StratState.get(agent, %{})
      config = state[:config]

      assert config.system_prompt == "You are a helpful testing assistant."
    end

    test "false system_prompt is treated as omitted" do
      default_config = StratState.get(BasicAgent.new(), %{})[:config]
      config = StratState.get(AgentWithFalseSystemPrompt.new(), %{})[:config]

      assert config.system_prompt == default_config.system_prompt
    end

    test "nil system_prompt is treated as omitted" do
      default_config = StratState.get(BasicAgent.new(), %{})[:config]
      config = StratState.get(AgentWithNilSystemPrompt.new(), %{})[:config]

      assert config.system_prompt == default_config.system_prompt
    end

    test "raises when module attribute system_prompt does not resolve to a binary" do
      module_name = Module.concat(__MODULE__, :"InvalidPromptAgent#{System.unique_integer([:positive, :monotonic])}")

      source = """
      defmodule #{inspect(module_name)} do
        @prompt 123

        use Jido.AI.Agent,
          name: "invalid_prompt_agent",
          tools: [#{inspect(TestCalculator)}],
          system_prompt: @prompt
      end
      """

      assert_raise CompileError, ~r/system_prompt must be a binary, nil, false/, fn ->
        Code.compile_string(source)
      end
    end

    test "tools list resolves module aliases" do
      agent = BasicAgent.new()
      tools = ReAct.list_tools(agent)

      # Should be actual module atoms, not AST
      assert TestCalculator in tools
      assert TestSearch in tools
      assert Enum.all?(tools, &is_atom/1)
    end

    test "does not warn when consumer defines its own thinking_meta/1" do
      module_name = Module.concat(__MODULE__, :"CollisionAgent#{System.unique_integer([:positive, :monotonic])}")

      source = """
      defmodule #{inspect(module_name)} do
        use Jido.AI.Agent,
          name: "collision_agent",
          tools: [#{inspect(TestCalculator)}]

        defp thinking_meta(_), do: %{custom: true}
      end
      """

      warnings =
        capture_io(:stderr, fn ->
          Code.compile_string(source)
        end)

      refute warnings =~ "this clause for thinking_meta/1 cannot match"
    end
  end

  # ============================================================================
  # ask/3 with Per-Request Tool Context
  # ============================================================================

  describe "ask/3 with tool_context option" do
    test "ask/2 works without options" do
      # We can't fully test without starting a server, but we can verify the function exists
      assert function_exported?(BasicAgent, :ask, 2)
      assert function_exported?(BasicAgent, :ask, 3)
    end

    test "ask/3 accepts tool_context option" do
      # The function signature should accept opts
      # This is a compile-time check - the function is generated by the macro
      assert :erlang.fun_info(&BasicAgent.ask/3, :arity) == {:arity, 3}
    end

    test "ask_stream/3 exists as stream wrapper" do
      assert :erlang.fun_info(&BasicAgent.ask_stream/3, :arity) == {:arity, 3}
    end
  end

  describe "request lifecycle hooks" do
    test "on_before_cmd marks request as failed on react_request_error" do
      agent = BasicAgent.new()
      agent = Request.start_request(agent, "req_1", "query", stream_to: {:pid, self()})
      tag = Request.Stream.message_tag()

      {:ok, agent, _action} =
        BasicAgent.on_before_cmd(
          agent,
          {:ai_react_request_error, %{request_id: "req_1", reason: :busy, message: "busy"}}
        )

      assert get_in(agent.state, [:requests, "req_1", :status]) == :failed
      assert get_in(agent.state, [:requests, "req_1", :error]) == {:rejected, :busy, "busy"}

      assert_receive {^tag,
                      %Event{
                        kind: :request_failed,
                        request_id: "req_1",
                        data: %{error: {:rejected, :busy, "busy"}, reason: :busy}
                      }}
    end

    test "on_after_cmd cancel does not overwrite completed request" do
      agent = BasicAgent.new()
      agent = Request.start_request(agent, "req_1", "query")
      agent = Request.complete_request(agent, "req_1", "done")

      {:ok, agent, _directives} =
        BasicAgent.on_after_cmd(
          agent,
          {:ai_react_cancel, %{request_id: "req_1", reason: :user_cancelled}},
          []
        )

      assert get_in(agent.state, [:requests, "req_1", :status]) == :completed
      assert get_in(agent.state, [:requests, "req_1", :result]) == "done"
      assert get_in(agent.state, [:requests, "req_1", :error]) == nil
    end

    test "on_after_cmd keeps last_answer string while request failure stores raw term" do
      raw_error = %{type: :provider_error, status: 503, message: "try later"}

      agent =
        BasicAgent.new()
        |> Request.start_request("req_failed", "query")
        |> with_failed_strategy(raw_error)

      {:ok, updated_agent, directives} =
        BasicAgent.on_after_cmd(
          agent,
          {:ai_react_worker_event, %{request_id: "req_failed", event: %{request_id: "req_failed"}}},
          [:noop]
        )

      assert directives == [:noop]
      assert get_in(updated_agent.state, [:requests, "req_failed", :status]) == :failed
      assert get_in(updated_agent.state, [:requests, "req_failed", :error]) == {:failed, :provider_error, raw_error}
      assert updated_agent.state.last_answer == inspect(raw_error)
      assert updated_agent.state.completed == true
    end

    test "on_after_cmd stores enriched request meta from the ReAct snapshot" do
      reasoning_details = [%{signature: "sig_123", provider: :openai}]
      thinking_trace = [%{call_id: "call_1", iteration: 1, thinking: "Step by step..."}]

      agent =
        BasicAgent.new()
        |> Request.start_request("req_meta", "query")
        |> with_completed_strategy("final answer", %{
          usage: %{input_tokens: 7, output_tokens: 3, reasoning_tokens: 18},
          thinking_trace: thinking_trace,
          streaming_thinking: "Final reasoning",
          run_context:
            AIContext.new()
            |> AIContext.append_user("query")
            |> AIContext.append_assistant("final answer", nil, reasoning_details: reasoning_details)
        })

      {:ok, updated_agent, _directives} =
        BasicAgent.on_after_cmd(agent, {:ai_react_start, %{request_id: "req_meta"}}, [])

      request = get_in(updated_agent.state, [:requests, "req_meta"])
      assert request.status == :completed
      assert request.meta.usage.reasoning_tokens == 18
      assert request.meta.reasoning_details == reasoning_details
      assert request.meta.thinking_trace == thinking_trace
      assert request.meta.last_thinking == "Final reasoning"
    end
  end

  # ============================================================================
  # tools_from_skills/1 Tests
  # ============================================================================

  describe "tools_from_skills/1" do
    defmodule MockSkill do
      def actions, do: [TestCalculator, TestSearch]
    end

    defmodule MockSkill2 do
      def actions, do: [TestSearch]
    end

    test "extracts actions from skill modules" do
      tools = Agent.tools_from_skills([MockSkill])

      assert TestCalculator in tools
      assert TestSearch in tools
    end

    test "deduplicates actions from multiple skills" do
      tools = Agent.tools_from_skills([MockSkill, MockSkill2])

      # Should have unique entries only
      assert length(Enum.filter(tools, &(&1 == TestSearch))) == 1
    end

    test "returns empty list for empty input" do
      assert Agent.tools_from_skills([]) == []
    end
  end

  defp with_failed_strategy(agent, result) do
    strategy_state = %{status: :error, result: result, termination_reason: :provider_error}
    put_in(agent.state[:__strategy__], strategy_state)
  end

  defp with_completed_strategy(agent, result, overrides) do
    strategy_state =
      agent.state[:__strategy__]
      |> Map.merge(%{status: :completed, result: result})
      |> Map.merge(overrides)

    put_in(agent.state[:__strategy__], strategy_state)
  end
end
